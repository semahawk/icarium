start: instr*
instr: /nop/         ["." cond] -> nop
     | /jump/        ["." cond] imm -> jump
     | /set/         ["." cond] reg "," imm ["shl" imm] -> set
     | /load/        ["." cond] reg "," reg ["off" imm] -> load
     | /store/       ["." cond] reg "," reg ["off" imm] -> store
     | /testbit/     ["." cond] reg "," imm -> testbit
     | /sub/         ["." cond] reg "," imm -> sub
     | /shiftl/      ["." cond] reg "," imm -> shiftl
     | /or/          ["." cond] reg "," reg -> or_
     | /xor/         ["." cond] reg "," reg -> xor
     | /halt/        ["." cond] -> halt
     | /\.base/ imm -> base
     | /\.define/ NAME imm -> define
     | /\.include/ NAME -> include
     | /\.alias/ NAME reg -> alias
     | /([a-zA-Z_][a-zA-Z0-9_]+):/ -> label

!cond: /(z|nz)/ -> cond

!reg: /r[0-9]+/ -> reg
    | NAME -> reg
    | "pc" -> reg

!sep: /[\\n;]/

!imm: /0b[0-1]+/ -> imm
    | /0c[0-7]+/ -> imm
    | /(0d)?[0-9]+/ -> imm
    | /0x[0-9a-fA-F]+/ -> imm
    | /#([a-zA-Z_][a-zA-Z0-9_]+)/ -> special_imm

NAME: /[a-zA-Z_][a-zA-Z0-9_]*/

COMMENT: "//" /[^\n]/*

%import common.WS -> WS
%ignore COMMENT
%ignore WS
